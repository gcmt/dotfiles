_pkgname=i3lock
pkgname=i3lock-patched
pkgver=2.11
pkgrel=1
pkgdesc="An improved screenlocker based upon XCB and PAM"
arch=('i686' 'x86_64')
url="https://i3wm.org/i3lock/"
license=('MIT')
provides=('i3lock')
conflicts=('i3lock')
depends=('xcb-util-image' 'libev' 'cairo' 'libxkbcommon-x11')
backup=("etc/pam.d/i3lock")
makedepends=('git')
_patches=("i3lock.patch")
source=(
	"git://github.com/i3/i3lock#branch=master"
	"${_patches[@]}"
	"lock.png"
)
sha1sums=('SKIP'
          '525edd6e586ef65888645ff3a98343a43cfcee67'
          'd6301099a4f201d80f9573df14437388a7dde609')


pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --tags | sed 's/-/./g'
}


prepare() {
	cd "$_pkgname"

	for patch in "${_patches[@]}"; do
		echo "Applying patch $patch..."
		patch -p1 -i "$srcdir/$patch"
	done
}

build() {
	cd "$_pkgname"

	# Fix ticket FS#31544, sed line taken from gentoo
	sed -i -e 's:login:system-auth:' pam/i3lock

	autoreconf --force --install

	rm -rf build/
	mkdir -p build && cd build/

	../configure --prefix=/usr --sysconfdir=/etc --disable-sanitizers
}


package() {
	cd "$_pkgname"
	cd build/

	make DESTDIR="$pkgdir/" install

	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
	install -Dm644 "$srcdir/lock.png" "$pkgdir/usr/share/$_pkgname/resources/lock.png"
}
