_pkgname=i3lock
pkgname=i3lock-patched
pkgver=2.11.1.g0c4b294
pkgrel=1
pkgdesc="An improved screenlocker based upon XCB and PAM"
arch=('i686' 'x86_64')
url="https://i3wm.org/i3lock/"
license=('MIT')
provides=('i3lock')
conflicts=('i3lock')
depends=('xcb-util-image' 'libev' 'cairo' 'libxkbcommon-x11')
backup=("etc/pam.d/i3lock")
makedepends=('git')
source=(
	"git://github.com/gcmt/i3lock#branch=custom-ui"
)
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --tags | sed 's/-/./g'
}

build() {
	cd "$_pkgname"

	# Fix ticket FS#31544, sed line taken from gentoo
	sed -i -e 's:login:system-auth:' pam/i3lock

	autoreconf --force --install

	rm -rf build/
	mkdir -p build && cd build/

	../configure --prefix=/usr --sysconfdir=/etc --disable-sanitizers

	make CPPFLAGS+="-U_FORTIFY_SOURCE"
}


package() {
	cd "$_pkgname"
	cd build/

	make DESTDIR="$pkgdir/" install

	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
