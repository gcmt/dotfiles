pkgname=st
_revision=1f24bde
pkgver=0.7.r41.g1f24bde
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
url="http://st.suckless.org"
_patches=(
	'st-disable-bold-italic-fonts.diff'
	'st-hidecursor-20170404-745c40f.diff'
	'st-scrollback-20170329-149c0d3.diff'
	'st-scrollback-mouse-20170427-5a10aca.diff'
	'st-scrollback-mouse-altscreen-20170427-5a10aca.diff'
)
source=(
	$pkgname::git://git.suckless.org/st
	"${_patches[@]}"
	config.h
)
md5sums=('SKIP'
         'b74a89c57f7131cc723e3f93b4ae03ff'
         '487dc42b84ea464474aac527c2b47191'
         'd858964e5123f6279a6f45ad57131ad7'
         '289cecdaa5421208de623958ee727614'
         '7c45639ae262c77b8aadd2b8d44d92fe'
         '2565e77f94e5269ebd6052ba4df88a1c')

pkgver() {
	cd "$pkgname"
	git describe --tags ${_revision} | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$pkgname"
	git checkout ${_revision}
	for patch in "${_patches[@]}"; do
		echo "Applying patch $patch..."
		patch -p1 -i "$srcdir/$patch"
	done
	cp "$srcdir/config.h" config.h
}

build() {
	cd "$pkgname"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir/" TERMINFO="$pkgdir/usr/share/terminfo" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
